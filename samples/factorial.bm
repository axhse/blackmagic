* Returns 1.
getOne x: = 1;

* Returns the factorial of a number.
factorial n:
    * Change n to 1 if it's 0.
    n = ifElse (equals n 0) 1 n;
    * Check if n is a valid value.
    isValid = ifElse (is n #integer) (notLess n 0) false;
    * Assert n is a valid value.
    maybeError = assert isValid 'Factorial can be calculated only for non-negative integers.';
    * Replace factorial function to a void function on error to prevent recursion.
    delegate = ifElse isValid factorial doNothing;
    * Calculate and return the result.
    = ifNoError maybeError (multiply n (ifElse (equals n 1) getOne delegate (decrement n)));

* Test.
testFactorial:
    factorial0 = factorial 0;
    factorial5 = factorial 5;
    error = factorial -1;
    result = join (toString factorial5) newLine;
    result = join result (toString factorial0);
    result = join result newLine;
    result = join result (toString error);
    result = join result newLine;
    assertion1 = assert (equals 1 factorial0) '0! = 1.';
    assertion2 = assert (equals 120 factorial5) '5! = 120.';
    assertion3 = assert (and (is error #error) (equals (getErrorType error) 'assertion')) '(-1)! may not be calculated.';
    result = ifNoError assertion3 result;
    result = ifNoError assertion2 result;
    result = ifNoError assertion1 result;
    = result;
